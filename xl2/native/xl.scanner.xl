// ****************************************************************************
//  xl.scanner.xl                   (C) 1992-2003 Christophe de Dinechin (ddd) 
//                                                                 XL2 project 
// ****************************************************************************
// 
//   File Description:
// 
//     The scanner for the bootstrap compiler
// 
// 
// 
// 
// 
// 
// 
// 
// ****************************************************************************
// This program is released under the GNU General Public License.
// See http://www.gnu.org/copyleft/gpl.html for details
// ****************************************************************************
// * File       : $RCSFile$
// * Revision   : $Revision$
// * Date       : $Date$
// ****************************************************************************

import IO = XL.TEXT_IO
import ERR = XL.ERRORS
import ASCII = XL.TEXT_IO.ENCODING.ASCII


module XL.SCANNER is

    function DigitValue(digit : character) return integer is
    // ------------------------------------------------------------------------
    //   Return the value of the given character
    // ------------------------------------------------------------------------
    // This implementation is actually specific to the ASCII encoding
        if digit >= '0' and digit <= '9' then
            return integer digit - integer '0'
        if digit >= 'A' and digit <= 'Z' then
            return integer digit - integer 'A' + 10
        if digit >= 'a' and digit <= 'z' then
            return integer digit - integer 'a' + 10
        return 999



    function Open(file_name : text) return scanner is
    // ------------------------------------------------------------------------
    //   Create and initialize a new scanner
    // ------------------------------------------------------------------------
        result.input := 0
        result.token := ""
        result.string_value := ""
        result.real_value := 0.0
        result.integer_value := 0
        result.indent_char := character(0)
        result.checking_indent := false
        result.setting_indent := false
        result.indent := 0
        result.position := 0
        result.line_start := 0

        result.input := IO.open(file_name)
        result.indents += 0


    procedure Close(S : scanner) is
    // ------------------------------------------------------------------------
    //   Close a scanner
    // ------------------------------------------------------------------------
        IO.Close S.input
        delete S


    procedure NextChar (S : scanner; in out C : character) is
    // ------------------------------------------------------------------------
    //   Read the next character
    // ------------------------------------------------------------------------
        S.token += C
        S.string_value += C
        IO.read S.input, C
        S.position += 1


    procedure NextLowerChar (S : scanner; in out C : character) is
    // ------------------------------------------------------------------------
    //   Read the next character
    // ------------------------------------------------------------------------
        S.token += ASCII.tolower(C)
        S.string_value += C
        IO.read S.input, C
        S.position += 1


    procedure IgnoreChar (S : scanner; in out C : character) is
    // ------------------------------------------------------------------------
    //   Read the next character
    // ------------------------------------------------------------------------
        S.string_value += C
        IO.read S.input, C
        S.position += 1


    function NextToken(S : scanner) return token is
    // ------------------------------------------------------------------------
    //    Parse the file until we get a complete token
    // ------------------------------------------------------------------------
        S.token := ""
        S.string_value := ""
        S.integer_value := 0
        S.real_value := 0.0
        S.base := 0

        // Check if file was opened correctly, or if we are at end of file
        if not IO.valid S.input then
            return tokEOF

        // Check if we unindented far enough for multiple indents
        if back S.indents > S.indent then
            pop_back S.indents
            return tokUNINDENT

        // Read next character
        C : character
        IO.Read S.input, C
        S.position += 1

        // Skip spaces and check indentation
        while ASCII.isspace C and IO.valid S.input loop
            if ASCII.is_line_break C then
                // New line: start counting indentation
                S.checking_indent := true
                S.line_start := S.position
            else if S.checking_indent then
                // Can't mix tabs and spaces
                if ASCII.is_space C then
                    if S.indent_char = 0 then
                        S.indent_char := C
                    else if S.indent_char <> C then
                        ERR.Error "Mixed tabs and spaces in indentation",
                                  S.position
            // Keep looking for more spaces
            IO.Read S.input, C
            S.position += 1

        // Stop counting indentation
        if S.checking_indent then
            IO.PutBack S.input, C
            S.position -= 1
            S.checking_indent := false
            column : integer := S.position - S.line_start
            if S.setting_indent then
                // We set a new indent, for instance after opening paren
                S.indents += S.indent
                S.indent := column
                S.setting_indent := false
                return tokNEWLINE
            else if column > S.indent then
                // Strictly deeper indent
                S.indent := column
                S.indents += S.indent
                return tokINDENT
            else if column < back S.indents then
                // Unindenting: remove rightmost indent level
                pop_back S.indents
                S.indent := column

                // If we unindented, but did not go as far as the
                // most recent indent, report inconsistency
                if back S.indents < column then
                    ERR.Error "Inconsistent indentation", S.position
                    return tokERROR

                // Otherwise, report that we undindented
                // We may report multiple tokUNINDENT if we unindented deep
                return tokUNINDENT

            else
                // Exactly the same indent level as before
                return tokNEWLINE

        // Report end of file if that's what we've got
        if not IO.valid S.input then
            return tokEOF

        // Look for numbers
        if ASCII.is_digit C then
            floating_point : boolean := false
            based_number   : boolean := false
            overflowed     : integer := 0

            S.base := 10
            S.integer_value := 0
            S.real_value := 0.0

            // Take integral part or base
            loop
                digit      : integer := DigitValue C
                while IO.valid S.input and digit < S.base loop
                    S.integer_value := S.base * S.integer_value + digit
                    S.real_value := S.base * S.real_value + digit
                    NextChar S, C
                    digit := DigitValue C

                    // Hmm, is this the best way to check for overflow?
                    if S.integer_value < 0 and overflowed = 0 then
                        overflowed := S.position

                    // If this looks like a digit, but outside the base, error
                    if C <> 'e' and C <> 'E' then
                        if digit <> 999 and digit >= S.base then
                            ERR.Error "Digit is invalid for the base",
                                      S.position
                            S.base := digit + 1

                    // Skip a single underscore
                    if C = '_' then
                        IgnoreChar S, C
                        digit := DigitValue C
                        if digit >= S.base then
                            ERR.Error "Invalid use of '_' in number",
                                      S.position

                    // Check if this is a based number
                    if based_number then
                        if digit >= S.base then
                            based_number := false
                    else if C = '#' then
                        S.base := S.integer_value
                        if S.base < 2 or S.base > 36 then
                            S.base := 36
                            ERR.Error "Invalid base (should be 2..36)",
                                      S.position
                        NextChar S, C
                        S.integer_value := 0
                        S.real_value := 0.0
                        based_number := true
                        digit := DigitValue C

                exit if not based_number or not IO.valid S.input

            // Check for fractional part
            if C = '.' then
                IO.Read S.input, C
                S.position += 1
                if DigitValue C >= S.base then
                    // This is something else following an integer
                    // For instance 1..3
                    IO.PutBack S.input, C
                    IO.PutBack S.input, '.'
                    S.position -= 2
                    return tokINTEGER
                else
                    S.token += '.'
                    S.string_value += '.'
                    floating_point := true

                    comma_position : real := 1.0
                    while IO.valid S.input and DigitValue C < S.base loop
                        comma_position /= S.base
                        S.real_value += comma_position * DigitValue C
                        NextChar S, C
                        if C = '_' then
                            IgnoreChar S, C
                            if DigitValue(C) >= S.base then
                                ERR.Error "Invalid use of '_' in real number",
                                          S.position

            // Check if we have a second '#' at end of based number
            if C = '#' then
                NextChar S, C

            // Check for the exponent
            if C = 'e' or C = 'E' then
                NextChar S, C

                exponent : integer := 0
                negative_exponent : boolean := false
                had_exponent : boolean := false

                // Exponent sign
                if C = '+' then
                    NextChar S, C
                else if C = '-' then
                    NextChar S, C
                    negative_exponent := true
                    floating_point := true

                // Exponent value
                while IO.valid S.input and DigitValue C < 10 loop
                    exponent := 10 * exponent + DigitValue C
                    had_exponent := true
                    NextChar S, C
                    if C = '_' then
                       IgnoreChar S, C
                       if DigitValue C >= 10 then
                           ERR.Error "Invalid use of '_' in number exponent",
                                     S.position

                // Check that we had an exponent
                if not had_exponent then
                    ERR.Error "Missing exponent", S.position

                // Compute base ^ exponent
                exponent_value : real := 1.0
                multiplier : real := S.base

                while exponent <> 0 loop
                    if (exponent & 1) <> 0 then
                        exponent_value *= multiplier
                    exponent /= 2
                    multiplier *= multiplier

                // Compute actual value
                if negative_exponent then
                    S.real_value /= exponent_value
                else
                    S.real_value *= exponent_value
                S.integer_value := integer S.real_value
            if not floating_point then
                if overflowed > 0 then
                    ERR.Error "Integer overflow", S.position
                else if (real(S.integer_value) < S.real_value - 1.0 or
                    real(S.integer_value) > S.real_value + 1.0) then
                    ERR.Error "Exponent overflow", S.position
            else if not (S.real_value = S.real_value) then
                ERR.Error "Numeric overflow", S.position

            // Return the last token
            IO.PutBack S.input, C
            S.position -= 1
            if floating_point then
                return tokREAL
            return tokINTEGER
        // End of numbers

        // Look for names
        if ASCII.is_letter C then
            while IO.valid S.input and ASCII.is_letter_or_digit C loop
                NextLowerChar S, C
                if C = '_' then
                    IgnoreChar S, C
                    if not ASCII.is_letter_or_digit C then
                        ERR.Error "Invalid use of '_' in name", S.position
            IO.PutBack S.input, C
            S.position -= 1
            return tokNAME

        // Look for strings
        if ASCII.is_quote C then
            eos : character := C
            S.token := C
            IO.Read S.input, C
            S.position += 1
            while IO.valid S.input loop
                // Check end of string, but keep double quotes in
                if C = eos then
                    S.token += C
                    IO.Read S.input, C
                    S.position += 1
                    if C <> eos then
                        IO.PutBack S.input, C
                        S.position -= 1
                        return tokTEXT

                if not IO.Valid S.input or ASCII.is_line_break(C) then
                    ERR.Error "End of line in a string", S.position-1
                    return tokERROR
                NextChar S, C

        // Look for parentheses
        blk : text := text("") + C
        if count(S.blocks, blk) > 0 then
            S.string_value := C
            S.token := C
            if S.blocks[blk] <> "" then
                return tokPAROPEN
            return tokPARCLOSE

        // Other symbols
        while IO.valid S.input and ASCII.is_punctuation C loop
            exit if ASCII.is_quote C
            exit if count(S.blocks, text("") + C) > 0
            NextChar S, C
        IO.PutBack S.input, C
        S.position -= 1
        return tokSYMBOL


    function Comment(S : scanner; EndOfComment : text) return text is
    // ------------------------------------------------------------------------
    //   Scan a comment until the given end
    // ------------------------------------------------------------------------
        max     : integer   := length EndOfComment
        index   : integer   := 0
        c       : character := 0
        comment : text      := ""
        skip    : boolean   := false
        initcol : integer   := S.position - S.line_start

        while IO.valid S.input and index < max loop
            IO.Read S.input, C
            S.position += 1
            skip := false

            if ASCII.is_line_break C then
                // New-line: start counting indentation
                S.checking_indent := true
                S.line_start := S.position
            else if S.checking_indent then
                if ASCII.is_space C then
                    skip := S.position - S.line_start <= initcol
                else
                    S.checking_indent := false

            if C = EndOfComment[index] then
                index += 1
            else
                index := 0

            if not skip then
                comment += C

        if index >= max then
            comment := comment [0..length(comment) - max - 1]

        return comment


    function CurrentPosition(S : scanner) return integer is
    // ------------------------------------------------------------------------
    //   Return the current character count from start of file of a scanner
    // ------------------------------------------------------------------------
        return S.position


    procedure SetPosition(in out S : scanner; P : integer) is
    // ------------------------------------------------------------------------
    //   Set the current character count from start of file of a scanner
    // ------------------------------------------------------------------------
        S.position := P


    function OpenParen(S : scanner) return integer is
    // ------------------------------------------------------------------------
    //   Opening some parenthese: remember the setting_indent value
    // ------------------------------------------------------------------------
        if S.setting_indent then
            result := bitwise_not S.indent
        else
            result := S.indent
        S.setting_indent := true


    procedure CloseParen (S : scanner; old_indent : integer) is
    // ------------------------------------------------------------------------
    //    Closing some parenthese: reset the setting_indent value
    // ------------------------------------------------------------------------
        if old_indent < 0 then
            S.indent := bitwise_not old_indent
            if not S.setting_indent then
                if S.indent = back S.indents then
                    pop_back S.indents
            S.setting_indent := true
        else
            S.indent := old_indent
            if not S.setting_indent then
                if S.indent = back S.indents then
                    pop_back S.indents
            S.setting_indent := false
