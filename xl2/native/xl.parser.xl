// *****************************************************************************
// xl.parser.xl                                                       XL project
// *****************************************************************************
//
// File description:
//
//     The basic XL0 parser
//
//
//
//
//
//
//
//
// *****************************************************************************
// This software is licensed under the GNU General Public License v3+
// (C) 2003-2005,2007-2009,2015,2019-2020, Christophe de Dinechin <christophe@dinechin.org>
// *****************************************************************************
// This file is part of XL
//
// XL is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License,
// or (at your option) any later version.
//
// XL is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with XL, in a file named COPYING.
// If not, see <https://www.gnu.org/licenses/>.
// *****************************************************************************

import PT = XL.PARSER.TREE
import SC = XL.SCANNER
import ASCII = XL.TEXT_IO.ENCODING.ASCII
import ERR = XL.ERRORS
import POS = XL.SCANNER.POSITION
import XLT = XL.TRANSLATOR

module XL.PARSER is

    function Open(name : text; syntax : text) return parser is
    // ------------------------------------------------------------------------
    //   Create a new parser
    // ------------------------------------------------------------------------
        S : SC.scanner := SC.Open(name)
        result.scanner := S
        if syntax = "" then
            syntax := XLT.RuntimePath(XLT.syntax_file)
        ReadSyntaxFile result, syntax
        SC.SetPosition result.scanner, POS.OpenFile name
        S.blocks := result.blocks
        result.pending_token := SC.tokNONE


    procedure Close(P : parser) is
    // ------------------------------------------------------------------------
    //   Close a parser
    // ------------------------------------------------------------------------
        POS.CloseFile SC.CurrentPosition(P.scanner)
        SC.Close P.scanner
        delete P


    function IsValid(P : parser) return boolean is
    // ------------------------------------------------------------------------
    //   Return true if a parser appears valid
    // ------------------------------------------------------------------------
        if P <> nil then
            S : SC.scanner := P.scanner
            if S <> nil then
                if S.input <> nil then
                    return true
        return false


    type pending is record with
    // ------------------------------------------------------------------------
    //    Pending expression while parsing
    // ------------------------------------------------------------------------
        opcode             : text
        argument           : PT.tree
        priority           : integer
        position           : integer


    function Parse(P : parser) return PT.tree is
    // ------------------------------------------------------------------------
    //   The top-level parsing function
    // ------------------------------------------------------------------------
        return Parse(P, "")


    function IsComment(P: parser; sym: text; out eoc: text) return boolean is
    // ------------------------------------------------------------------------
    //   Check if a symbol represents a comment, if so return end of comment
    // ------------------------------------------------------------------------
        if count (P.comments, sym) > 0 then
            eoc := P.comments[sym]
            return true
        return false


    function IsText(P: parser; sym: text; out eot: text) return boolean is
    // ------------------------------------------------------------------------
    //   Check if a symbol represents a text, if so return end of text
    // ------------------------------------------------------------------------
        if count (P.text_delimiters, sym) > 0 then
            eot := P.text_delimiters[sym]
            return true
        return false


    function GetPrefixPriority(P: parser; sym : text) return integer is
    // ------------------------------------------------------------------------
    //   Return the prefix priority or parser's default priority
    // ------------------------------------------------------------------------
        if count(P.prefix_priority, sym) > 0 then
            return P.prefix_priority[sym]
        return P.default_priority


    function GetPostfixPriority(P: parser; sym : text) return integer is
    // ------------------------------------------------------------------------
    //   Return the postfix priority or parser's default priority
    // ------------------------------------------------------------------------
        if count(P.postfix_priority, sym) > 0 then
            return P.postfix_priority[sym]
        return P.default_priority


    function GetInfixPriority(P: parser; sym : text) return integer is
    // ------------------------------------------------------------------------
    //   Return the infix priority or parser's default priority
    // ------------------------------------------------------------------------
        if count(P.infix_priority, sym) > 0 then
            return P.infix_priority[sym]
        return P.default_priority


    function NextToken(P : parser) return SC.token is
    // ------------------------------------------------------------------------
    //   Read next token, skipping comments and gathering long text
    // ------------------------------------------------------------------------
        loop
            pending : SC.token := P.pending_token
            if pending <> SC.tokNONE then
                if pending <> SC.tokNEWLINE then
                    P.pending_token := SC.tokNONE
                    return pending

            // If we arrive here, there's nothing pending, or only a newline
            result := SC.NextToken(P.scanner)
            if result = SC.tokNAME or result = SC.tokSYMBOL then
                S    : SC.scanner := P.scanner
                name : text := S.token
                eot  : text
                if IsComment(P, name, eot) then
                    // Skip comments, and keep looking to get right indent
                    name := SC.Comment (S, eot)
                    if eot = ASCII.CR and pending = SC.tokNONE then
                        P.pending_token := SC.tokNEWLINE
                    restart
                else if IsText (P, name, eot) then
                    S.string_value := SC.Comment (S, eot)
                    if pending = SC.tokNEWLINE then
                        P.pending_token := SC.tokTEXT
                        return SC.tokNEWLINE
                    if eot = ASCII.CR and pending = SC.tokNONE then
                        P.pending_token := SC.tokNEWLINE
                    return SC.tokTEXT

                // If the next token has a substatement infix priority
                // this takes over any pending newline. Example: else
                if pending = SC.tokNEWLINE then
                    prefixp : integer := GetPrefixPriority(P, name)
                    if prefixp = P.default_priority then
                        infixp  : integer := GetInfixPriority(P, name)
                        if infixp < P.statement_priority then
                            P.pending_token := SC.tokNONE
                            pending := SC.tokNONE

            else if result = SC.tokNEWLINE then
                // Combined with any previous pending indent
                P.pending_token := SC.tokNEWLINE
                restart
            else if result = SC.tokUNINDENT then
                // Add newline if no infix
                P.pending_token := SC.tokNEWLINE
                return result
            else if result = SC.tokINDENT then
                // Ignore pending newline in that case
                P.pending_token := SC.tokNONE
                return result

            // If we have another token here and a pending newline,
            // push other token back
            if pending <> SC.tokNONE then
                P.pending_token := result
                return pending

            return result


    function Parse(P : parser; closing : text) return PT.tree is
    // ------------------------------------------------------------------------
    //   The actual parsing function
    // ------------------------------------------------------------------------
    // XL parsing is not very difficult, but a bit unusual, because it is based
    // solely on dynamic information and not, for instance, on keywords.
    // Consider the following cases, where p is "prefix-op" and i is "infix-op"
    //     Write A
    //  Parses as p(Write,A).
    //     A and B
    //  Parses as i(and,A,B) if 'and' has a priority,
    //         as p(A,p(and,B)) otherwise
    //  Write -A,B
    //  This parses as (Write-A),B since "-" has a priority.
    //  I wish I could fix that one...
    //  The correct XL syntax is: Write (-A),B
    // We hope that semantic will catch such a case later and let us know...

        comment_end             : text
        left                    : PT.tree
        right                   : PT.tree
        infix                   : text
        name                    : text
        spelling                : text
        prefix                  : text          := ""
        tok                     : SC.token
        done                    : boolean       := false
        block_opening           : text
        block_closing           : text
        default_priority        : integer       := P.default_priority
        function_priority       : integer       := P.function_priority
        statement_priority      : integer       := P.statement_priority
        result_priority         : integer       := default_priority
        prefix_priority         : integer
        infix_priority          : integer
        postfix_priority        : integer
        paren_priority          : integer       := GetInfixPriority(P, closing)
        stack                   : string of pending
        is_expression           : boolean       := true
        new_statement           : boolean       := true
        S                       : SC.scanner    := P.scanner
        pos                     : integer       := 0
        line_continuation       : boolean       := false
        extra_indents           : integer       := 0

        // When inside a () block, we are in 'expression' mode right away
        if closing <> "" and paren_priority > statement_priority then
            new_statement := false
            is_expression := true

        while not done loop

            // Scan next token
            right := nil
            prefix_priority := default_priority
            infix_priority := default_priority
            tok := NextToken(P)

            // Check if we are dealing with a trailing operator
            if line_continuation then
                if tok = SC.tokINDENT then
                    tok := SC.tokNEWLINE
                    extra_indents += 1
            else if extra_indents > 0 then
                if tok = SC.tokUNINDENT then
                    extra_indents -= 1
                    tok := SC.tokNEWLINE

            // Check token result
            pos := SC.CurrentPosition(S)
            if tok = SC.tokEOF or tok = SC.tokERROR then
                done := true
                if closing <> "" and closing <> UNINDENT_MARKER then
                    ERR.Error "Unexpected end of text, expected '$1'",
                              S.position, closing
            else if tok = SC.tokNATURAL then
                right := PT.NewNatural(S.integer_value, pos)
                prefix_priority := function_priority
            else if tok = SC.tokREAL then
                right := PT.NewReal(S.real_value, pos)
                prefix_priority := function_priority
            else if tok = SC.tokTEXT then
                text_separator : character := S.token[0]
                if text_separator <> '''' then text_separator := '"'
                right := PT.NewText(S.string_value, text_separator, pos)
                prefix_priority := function_priority
                if result = nil and new_statement then
                    is_expression := false
            else if tok = SC.tokNAME or tok = SC.tokSYMBOL then
                name := S.token
                spelling := S.string_value
                if result = nil then
                    // If this is the very first thing we see
                    prefix_priority := GetPrefixPriority(P, name)
                    right := PT.NewName(spelling, pos)
                    if prefix_priority = default_priority then
                        prefix_priority := function_priority
                    if new_statement and tok = SC.tokNAME then
                        is_expression := false
                else if left <> nil then
                    // This is the right of an infix operator
                    // If we have "A and not B", where "not" has
                    // higher priority than "and", we want to
                    // parse this as "A and (not B)" rather than as
                    // "(A and not) B"
                    prefix_priority := GetPrefixPriority (P, name)
                    right := PT.NewName(spelling, pos)
                    if prefix_priority = default_priority then
                        prefix_priority := function_priority
                else
                    // Complicated case: need to discriminate
                    // infix and prefix
                    infix_priority := GetInfixPriority (P, name)
                    if infix_priority <> default_priority then
                        // We got an infix
                        left := result
                        infix := spelling
                    else
                        postfix_priority := GetPostfixPriority (P, name)
                        if postfix_priority <> default_priority then
                            // We have a postfix operator
                            right := PT.NewName(spelling, pos)
                            right := PT.NewPrefix(result, right, pos)
                            prefix_priority := postfix_priority
                            result := nil
                        else
                            // No priority : take this as a prefix by default
                            prefix_priority := GetPrefixPriority(P, name)
                            right := PT.NewName(spelling, pos)
                            if prefix_priority = default_priority then
                                prefix_priority := function_priority
                                if new_statement and tok = SC.tokNAME then
                                    is_expression := false

            else if tok = SC.tokNEWLINE then
                // Consider new-line as an infix operator
                infix := ASCII.CR
                name := infix
                infix_priority := GetInfixPriority (P, infix)
                left := result

            else if tok = SC.tokPARCLOSE then
                // Check for mismatched parentheses here
                if S.token <> closing then
                    ERR.Error "Mismatched parenthese: '$1', expected '$2'",
                              S.position, S.token, text(closing)
                done := true

            else if tok = SC.tokUNINDENT then
                // Check for mismatched parentheses here
                if closing <> UNINDENT_MARKER then
                    ERR.Error "Unexpected unindent, expected '$1'",
                              S.position, text(closing)
                done := true

            else if tok = SC.tokPAROPEN or tok = SC.tokINDENT then
                old_indent : integer
                if tok = SC.tokPAROPEN then
                    block_opening := S.token
                    block_closing := S.blocks[block_opening]
                    if  block_closing = "" then
                        ERR.Error "Mismatched parenthese: '$1' (internal)",
                                  S.position, S.token
                    old_indent := SC.OpenParen(S)
                else
                    block_opening := INDENT_MARKER
                    block_closing := UNINDENT_MARKER

                name := block_opening
                paren_priority := GetInfixPriority (P, name)

                // Just like for names, parse the contents of the parentheses
                prefix_priority := paren_priority
                infix_priority := default_priority
                right := Parse(P, block_closing)
                if tok = SC.tokPAROPEN then SC.CloseParen S, old_indent
                if right = nil then
                    right := PT.NewName ("", pos)
                right := PT.NewBlock(right, block_opening, block_closing, pos)
            else
                IO.writeln "Invalid token: ", tok, " (", S.token, ")"
                assert not "Invalid token"

            // Check what is the current result
            line_continuation := false
            if result = nil then

                // First thing we parse
                result := right
                result_priority := prefix_priority

                // We are now in the middle of an expression
                if result and result_priority >= statement_priority then
                    new_statement := false

            else if left <> nil then

                // Check if we had a statement separator
                if infix_priority < statement_priority then
                    new_statement := true
                    is_expression := true

                // We got left and infix-op, we are now looking for right
                // If we have 'A and not B', where 'not' has higher priority
                // than 'and', we want to finish parsing 'not B' first, rather
                // than seeing this as '(A and not) B'.
                if prefix_priority <> default_priority then
                    // Push "A and" in the above example
                    T : pending
                    T.opcode := infix
                    T.argument := left
                    T.priority := infix_priority
                    if new_statement then
                        T.position := left.position
                    else
                        T.position := pos
                    stack += T
                    if infix_priority > default_priority then
                        line_continuation := true
                    left := nil

                    // Start over with "not"
                    result := right
                    result_priority := prefix_priority

                else
                    while size(stack) loop
                        prev : pending := back(stack)

                        // Check priorities compared to stack
                        // A+B*C we got '*', keep 'A+..." on stack
                        // Make odd priorities right associative by
                        // clearing low-bit in previous priority
                        if not done then
                            exit if (prev.priority <> default_priority and
                                     infix_priority >
                                     (prev.priority & (bitwise_not 1)))
                        if prev.opcode = prefix then
                            left := PT.NewPrefix(prev.argument, left,
                                                 prev.position)
                        else
                            left := PT.NewInfix(prev.opcode, prev.argument,
                                                left, prev.position)
                        pop_back stack

                    // Now we want to restart with the rightmost operand
                    if done then
                        // End of text: the result is what we just got
                        result := left

                    else
                        // Something like A+B+C, just got second +
                        T : pending
                        T.opcode := infix
                        T.argument := left
                        T.priority := infix_priority
                        if new_statement then
                            T.position := left.position
                        else
                            T.position := pos
                        stack += T
                        if infix_priority > default_priority then
                            line_continuation := true
                        result := nil

                    left := nil

            else if right <> nil then

                // Check if we had a low-priority prefix (e.g. pragmas)
                if prefix_priority < statement_priority then
                    new_statement := true
                    is_expression := true

                // Check priorities for "A.B x,y" -> "(A.B) (x,y)"
                // Make odd priorities right associative by
                // clearing low-bit in previous priority
                if prefix_priority <= result_priority then
                    while size(stack) loop
                        prev : pending := back(stack)
                        if not done then
                            exit if (prev.priority <> default_priority and
                                     result_priority >
                                     (prev.priority & (bitwise_not 1)))

                        if prev.opcode = prefix then
                            result := PT.NewPrefix(prev.argument, result,
                                                   prev.position)
                        else
                            result := PT.NewInfix(prev.opcode, prev.argument,
                                                  result, prev.position)
                        pop_back stack

                // Check if new statement
                T : pending
                if not is_expression then
                    if right.kind <> PT.xlBLOCK then
                        if result_priority > statement_priority then
                            if size(stack) = 0 then
                                result_priority := statement_priority
                            else
                                T := back(stack)
                                if T.priority < statement_priority then
                                    result_priority := statement_priority

                // Push a recognized prefix op
                T.opcode := prefix
                T.argument := result
                T.priority := result_priority
                T.position := pos
                stack += T
                result := right
                result_priority := prefix_priority
        // end "while not done"

        if size(stack) then
            if result = nil then
                last : pending := back(stack)
                if last.opcode <> ASCII.CR then
                    ERR.Error "Trailing opcode '$1' ignored",
                              last.position, last.opcode
                result := last.argument
                pop_back stack

            // Check if some stuff remains on the stack
            while size(stack) loop
                prev : pending := back(stack)
                if prev.opcode = prefix then
                    result := PT.NewPrefix(prev.argument, result,
                                           prev.position)
                else
                    result := PT.NewInfix(prev.opcode,prev.argument,
                                          result,prev.position)
                pop_back stack


    procedure ReadSyntaxFile (P : PR.parser; syntax : text) is
    // ------------------------------------------------------------------------
    //   Read the xl.syntax file to get precedences
    // ------------------------------------------------------------------------
        type state is enumeration
            inUnknown, inInfix, inPrefix, inPostfix,
            inComment, inCommentDef,
            inText, inTextDef,
            inBlock, inBlockDef

        txt      : text
        entry    : text
        tok      : SC.token
        priority : integer    := 0
        S        : SC.scanner := SC.Open(syntax)
        stxst    : state      := inUnknown

        loop
            tok := SC.NextToken(S)
            exit if tok = SC.tokEOF

            if tok = SC.tokNATURAL then
                priority := S.integer_value

            else if (tok = SC.tokNAME or tok = SC.tokSYMBOL or
                     tok = SC.tokTEXT) then
                txt := S.string_value
                if txt = "NEWLINE" then
                    txt := ASCII.CR
                else if txt = "INDENT" then
                    txt := INDENT_MARKER
                else if txt = "UNINDENT" then
                    txt := UNINDENT_MARKER

                if txt = "INFIX" then
                    stxst := inInfix
                else if txt = "PREFIX" then
                    stxst := inPrefix
                else if txt = "POSTFIX" then
                    stxst := inPostfix
                else if txt = "BLOCK" then
                    stxst := inBlock
                else if txt = "COMMENT" then
                    stxst := inComment
                else if txt = "TEXT" then
                    stxst := inText
                else if txt = "STATEMENT" then
                    P.statement_priority := priority
                else if txt = "FUNCTION" then
                    P.function_priority := priority
                else if txt = "DEFAULT" then
                    P.default_priority := priority
                else if stxst = inPrefix then
                    P.prefix_priority[txt] := priority
                else if stxst = inPostfix then
                    P.postfix_priority[txt] := priority
                else if stxst = inInfix then
                    P.infix_priority[txt] := priority
                else if stxst = inComment then
                    entry := txt
                    stxst := inCommentDef
                else if stxst = inCommentDef then
                    P.comments[entry] := txt
                    stxst := inComment
                else if stxst = inText then
                    entry := txt
                    stxst := inTextDef
                else if stxst = inTextDef then
                    P.text_delimiters[entry] := txt
                    stxst := inText
                else if stxst = inBlock then
                    entry := txt
                    stxst := inBlockDef
                    P.infix_priority[entry] := priority
                else if stxst = inBlockDef then
                    P.infix_priority[txt] := priority
                    P.blocks[entry] := txt
                    P.blocks[txt] := ""
                    stxst := inBlock

        SC.Close S
