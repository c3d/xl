// ****************************************************************************
//  xl.parser.xl                    (C) 1992-2003 Christophe de Dinechin (ddd) 
//                                                                 XL2 project 
// ****************************************************************************
// 
//   File Description:
// 
//     The basic XL0 parser
// 
// 
// 
// 
// 
// 
// 
// 
// ****************************************************************************
// This program is released under the GNU General Public License.
// See http://www.gnu.org/copyleft/gpl.html for details
// ****************************************************************************
// * File       : $RCSFile$
// * Revision   : $Revision$
// * Date       : $Date$
// ****************************************************************************

import PT = XL.PARSER.TREE
import SC = XL.SCANNER
import ASCII = XL.TEXT_IO.ENCODING.ASCII
import ERR = XL.ERRORS
import POS = XL.SCANNER.POSITION

module XL.PARSER is

    function Open(name : text) return parser is
    // ------------------------------------------------------------------------
    //   Create a new parser
    // ------------------------------------------------------------------------
        S : SC.scanner := SC.Open(name)
        result.scanner := S
        ReadSyntaxFile result, "xl.syntax"
        SC.SetPosition result.scanner, POS.OpenFile name
        S.blocks := result.blocks


    procedure Close(P : parser) is
    // ------------------------------------------------------------------------
    //   Close a parser
    // ------------------------------------------------------------------------
        POS.CloseFile SC.CurrentPosition(P.scanner)
        SC.Close P.scanner
        delete P


    type pending is record with
    // ------------------------------------------------------------------------
    //    Pending expression while parsing
    // ------------------------------------------------------------------------
        opcode             : text
        argument           : PT.tree
        priority           : integer
        position           : integer


    function Parse(P : parser) return PT.tree is
    // ------------------------------------------------------------------------
    //   The top-level parsing function
    // ------------------------------------------------------------------------
        return Parse(P, "")


    function IsComment(P: parser; sym: text; out eoc: text) return boolean is
    // ------------------------------------------------------------------------
    //   Check if a symbol represents a comment, if so return end of comment
    // ------------------------------------------------------------------------
        if count (P.comments, sym) > 0 then
            eoc := P.comments[sym]
            return true
        return false


    function IsText(P: parser; sym: text; out eot: text) return boolean is
    // ------------------------------------------------------------------------
    //   Check if a symbol represents a text, if so return end of text
    // ------------------------------------------------------------------------
        if count (P.text_delimiters, sym) > 0 then
            eot := P.text_delimiters[sym]
            return true
        return false


    function NextToken(P : parser) return SC.token is
    // ------------------------------------------------------------------------
    //   Read next token, skipping comments and gathering long text
    // ------------------------------------------------------------------------
        loop
            result := SC.NextToken(P.scanner)
            if result = SC.tokNAME or result = SC.tokSYMBOL then
                S    : SC.scanner := P.scanner
                name : text := S.token
                eot  : text
                if IsComment(P, name, eot) then
                    // Skip comments
                    name := SC.Comment (S, eot)
                    restart
                else if IsText (P, name, eot) then
                    S.string_value := SC.Comment (S, eot)
                    return SC.tokSTRING
            return result


    function GetPrefixPriority(P: parser; sym : text) return integer is
    // ------------------------------------------------------------------------
    //   Return the prefix priority or parser's default priority
    // ------------------------------------------------------------------------
        if count(P.prefix_priority, sym) > 0 then
            return P.prefix_priority[sym]
        return P.default_priority


    function GetPostfixPriority(P: parser; sym : text) return integer is
    // ------------------------------------------------------------------------
    //   Return the postfix priority or parser's default priority
    // ------------------------------------------------------------------------
        if count(P.postfix_priority, sym) > 0 then
            return P.postfix_priority[sym]
        return P.default_priority


    function GetInfixPriority(P: parser; sym : text) return integer is
    // ------------------------------------------------------------------------
    //   Return the infix priority or parser's default priority
    // ------------------------------------------------------------------------
        if count(P.infix_priority, sym) > 0 then
            return P.infix_priority[sym]
        return P.default_priority


    function Parse(P : parser; closing : text) return PT.tree is
    // ------------------------------------------------------------------------
    //   The actual parsing function
    // ------------------------------------------------------------------------
    // XL parsing is not very difficult, but a bit unusual, because it is based
    // solely on dynamic information and not, for instance, on keywords.
    // Consider the following cases, where p is "prefix-op" and i is "infix-op"
    //     Write A
    //  Parses as p(Write,A).
    //     A and B
    //  Parses as i(and,A,B) if 'and' has a priority,
    //         as p(A,p(and,B)) otherwise
    //  Write -A,B
    //  This parses as (Write-A),B since "-" has a priority.
    //  I wish I could fix that one...
    //  The correct XL syntax is: Write (-A),B
    // We hope that semantic will catch such a case later and let us know...

        comment_end             : text
        left                    : PT.tree
        right                   : PT.tree
        infix                   : text
        name                    : text
        spelling                : text
        prefix                  : text          := ""
        tok                     : SC.token
        done                    : boolean       := false
        block_opening           : text
        block_closing           : text
        default_priority        : integer       := P.default_priority
        function_priority       : integer       := P.function_priority
        statement_priority      : integer       := P.statement_priority
        result_priority         : integer       := default_priority
        prefix_priority         : integer
        infix_priority          : integer
        postfix_priority        : integer
        paren_priority          : integer
        stack                   : string of pending
        is_expression           : boolean       := true
        new_statement           : boolean       := true
        pending_token           : SC.token      := SC.tokNONE
        S                       : SC.scanner    := P.scanner
        pos                     : integer       := 0
        line_continuation       : boolean       := false
        extra_indents           : integer       := 0

        while not done loop
            // If no token pending, scan one
            if pending_token = SC.tokNONE then
                right := nil
                prefix_priority := default_priority
                infix_priority := default_priority
                tok := NextToken(P)
            else if pending_token = SC.tokNEWLINE then
                // We have a pending new-line:

                // Skip actual token if it is also a new-line
                tok := NextToken(P)
                if tok = SC.tokNEWLINE then
                    restart

                // Check names and symbols, skip comments
                else if tok = SC.tokSYMBOL or tok = SC.tokNAME then
                    name := S.token
                    if IsComment(P, name, comment_end) then
                        // Got a comment with a pending new-line, skip it
                        SC.Comment S, comment_end
                        if comment_end = ASCII.CR then
                            restart

                    // Check if we got something like 'else'
                    if GetInfixPriority(P, name) >= statement_priority then
                        // Otherwise, delay current token and process
                        // the 'fake' new-line
                        pending_token := tok
                        tok := SC.tokNEWLINE
                    right := nil
                    prefix_priority := default_priority
                    infix_priority := default_priority

                // Something that is itself a new-line parses directly
                else if tok = SC.tokINDENT or tok = SC.tokEOF then
                    pending_token := SC.tokNONE
                    right := nil
                    prefix_priority := default_priority
                    infix_priority := default_priority

                // Something else is delayed until after we deal with new-line
                else
                    pending_token := tok
                    tok := SC.tokNEWLINE
                    right := nil
                    prefix_priority := default_priority
                    infix_priority := default_priority

            // Other pending tokens just fall through
            else
                tok := pending_token
                pending_token := SC.tokNONE

            // Check if we are dealing with a trailing operator
            if line_continuation then
                if tok = SC.tokINDENT then
                    tok := SC.tokNEWLINE
                    extra_indents += 1
            else if extra_indents > 0 then
                if tok = SC.tokUNINDENT then
                    extra_indents -= 1
                    tok := SC.tokNEWLINE

            // Check token result
            pos := SC.CurrentPosition(S)
            if tok = SC.tokEOF or tok = SC.tokERROR then
                done := true
            else if tok = SC.tokINTEGER then
                right := PT.NewInteger(S.integer_value, pos)
                prefix_priority := function_priority
            else if tok = SC.tokREAL then
                right := PT.NewReal(S.real_value, pos)
                prefix_priority := function_priority
            else if tok = SC.tokSTRING then
                right := PT.NewText(S.string_value, '"', pos)
                prefix_priority := function_priority
            else if tok = SC.tokQUOTE then
                right := PT.NewText(S.string_value, '''', pos)
                prefix_priority := function_priority
            else if tok = SC.tokNAME or tok = SC.tokSYMBOL then
                name := S.token
                spelling := S.string_value
                if result = nil then
                    // If this is the very first thing we see
                    prefix_priority := GetPrefixPriority(P, name)
                    right := PT.NewName(spelling, pos)
                    if prefix_priority = default_priority then
                        prefix_priority := function_priority
                    if new_statement and tok = SC.tokNAME then
                        is_expression := false
                else if left <> nil then
                    // This is the right of an infix operator
                    // If we have "A and not B", where "not" has
                    // higher priority than "and", we want to
                    // parse this as "A and (not B)" rather than as
                    // "(A and not) B"
                    prefix_priority := GetPrefixPriority (P, name)
                    right := PT.NewName(spelling, pos)
                    if prefix_priority = default_priority then
                        prefix_priority := function_priority
                else
                    // Complicated case: need to discriminate
                    // infix and prefix
                    infix_priority := GetInfixPriority (P, name)
                    if infix_priority <> default_priority then
                        // We got an infix or a postfix, compare priorities
                        postfix_priority := GetPostfixPriority (P, name)
                        if (postfix_priority <> default_priority and
                            postfix_priority >= infix_priority) then
                            // We have a postfix operator
                            right := PT.NewName(spelling, pos)
                            right := PT.NewPrefix(result, right, pos)
                            prefix_priority := postfix_priority
                            result := nil
                        else
                            left := result
                            infix := spelling
                    else
                        postfix_priority := GetPostfixPriority (P, name)
                        if postfix_priority <> default_priority then
                            // We have a postfix operator
                            right := PT.NewName(spelling, pos)
                            right := PT.NewPrefix(result, right, pos)
                            prefix_priority := postfix_priority
                            result := nil
                        else
                            // No priority : take this as a prefix by default
                            prefix_priority := GetPrefixPriority(P, name)
                            right := PT.NewName(spelling, pos)
                            if prefix_priority = default_priority then
                                prefix_priority := function_priority

            else if tok = SC.tokNEWLINE then
                if pending_token = SC.tokNONE then
                    tok := SC.NextToken S
                    if tok = SC.tokSYMBOL or tok = SC.tokNAME then
                        if IsComment(P, S.token, comment_end) then
                            // Followed by a comment
                            // Can't decide just yet what indent we have
                            // Skip the comment
                            SC.Comment S, comment_end
                            restart
                    // Otherwise, we'll deal with this other token next
                    pending_token := tok

                // Consider new-line as an infix operator
                infix := ASCII.CR
                name := infix
                infix_priority := GetInfixPriority (P, infix)
                left := result

            else if tok = SC.tokPARCLOSE then
                // Check for mismatched parentheses here
                if S.token <> closing then
                    ERR.Error "Mismatched parenthese: '$1', expected '$2'",
                              S.position, S.token, text(closing)
                done := true

            else if tok = SC.tokUNINDENT then
                // Check for mismatched parentheses here
                if closing <> UNINDENT_MARKER then
                    ERR.Error "Unexpected unindent, expected '$1'",
                              S.position, text(closing)
                done := true

            else if tok = SC.tokPAROPEN or tok = SC.tokINDENT then
                old_indent : integer
                if tok = SC.tokPAROPEN then
                    block_opening := S.token
                    block_closing := S.blocks[block_opening]
                    if  block_closing = "" then
                        ERR.Error "Mismatched parenthese: '$1' (internal)",
                                  S.position, S.token
                    old_indent := SC.OpenParen(S)
                else
                    block_opening := INDENT_MARKER
                    block_closing := UNINDENT_MARKER

                name := block_opening
                paren_priority := GetInfixPriority (P, name)

                // Just like for names, parse the contents of the parentheses
                prefix_priority := paren_priority
                infix_priority := default_priority
                right := Parse(P, block_closing)
                if tok = SC.tokPAROPEN then SC.CloseParen S, old_indent
                if right = nil then
                    right := PT.NewName ("", pos)
                right := PT.NewBlock(right, block_opening, block_closing, pos)

                if tok = SC.tokINDENT then
                    // Unindent to be seen as unindent followed by new-line
                    // so that new-line operator combines lines
                    pending_token := SC.tokNEWLINE

            else
                IO.writeln "Invalid token: ", tok, " (", S.token, ")"
                assert not "Invalid token"

            // Check what is the current result
            line_continuation := false
            if result = nil then

                // First thing we parse
                result := right
                if new_statement and not is_expression then
                    if prefix_priority = default_priority then
                        prefix_priority := statement_priority
                result_priority := prefix_priority

                // We are now in the middle of an expression
                if result then
                    new_statement := false

            else if left <> nil then

                // Check if we had a statement separator
                if infix_priority < statement_priority then
                    new_statement := true
                    is_expression := true

                // We got left and infix-op, we are now looking for right
                // If we have 'A and not B', where 'not' has higher priority
                // than 'and', we want to finish parsing 'not B' first, rather
                // than seeing this as '(A and not) B'.
                if (prefix_priority <> default_priority and
                    prefix_priority > infix_priority) then
                    // Push "A and" in the above example
                    T : pending
                    T.opcode := infix
                    T.argument := left
                    T.priority := infix_priority
                    if new_statement then
                        T.position := left.position
                    else
                        T.position := pos
                    stack += T
                    if infix_priority > default_priority then
                        line_continuation := true
                    left := nil

                    // Start over with "not"
                    result := right
                    result_priority := prefix_priority

                else
                    while size(stack) loop
                        prev : pending := back(stack)

                        // Check priorities compared to stack
                        // A+B*C we got '*', keep 'A+..." on stack
                        if not done then
                            exit if (prev.priority <> default_priority and
                                     infix_priority > prev.priority)
                            exit if (prev.priority < statement_priority and
                                     infix_priority = prev.priority)
                        if prev.opcode = prefix then
                            left := PT.NewPrefix(prev.argument, left,
                                                 prev.position)
                        else
                            left := PT.NewInfix(prev.opcode, prev.argument,
                                                left, prev.position)
                        pop_back stack

                    // Now we want to restart with the rightmost operand
                    if done then
                        // End of text: the result is what we just got
                        result := left

                    else
                        // Something like A+B+C, just got second +
                        T : pending
                        T.opcode := infix
                        T.argument := left
                        T.priority := infix_priority
                        if new_statement then
                            T.position := left.position
                        else
                            T.position := pos
                        stack += T
                        if infix_priority > default_priority then
                            line_continuation := true
                        result := nil

                    left := nil

            else if right <> nil then

                // Check priorities for "A.B x,y" -> "(A.B) (x,y)"
                right_assoc : boolean := right.kind <> PT.xlBLOCK
                while size(stack) loop
                    prev : pending := back(stack)
                    if not done then
                        exit if (prev.priority <> default_priority and
                                 result_priority > prev.priority)
                        exit if (prev.priority < statement_priority and
                                 result_priority = prev.priority)
                        exit if (right_assoc and
                                 prev.opcode = prefix and
                                 result_priority = prev.priority)

                    if prev.opcode = prefix then
                        result := PT.NewPrefix(prev.argument, result,
                                               prev.position)
                    else
                        result := PT.NewInfix(prev.opcode, prev.argument,
                                              result, prev.position)
                    pop_back stack

                // Check if new statement
                T : pending
                if right.kind <> PT.xlBLOCK then
                    if size(stack) = 0 then
                        if not is_expression then
                            result_priority := statement_priority
                    else
                        T := back(stack)
                        if T.priority < statement_priority then
                            result_priority := statement_priority

                // Push a recognized prefix op
                T.opcode := prefix
                T.argument := result
                T.priority := result_priority
                T.position := pos
                stack += T
                result := right
                result_priority := prefix_priority
        // end "while not done"

        if size(stack) then
            if result = nil then
                last : pending := back(stack)
                if last.opcode <> ASCII.CR then
                    ERR.Error "Trailing opcode '$1' ignored",
                              last.position, last.opcode
                result := last.argument
                pop_back stack

            // Check if some stuff remains on the stack
            while size(stack) loop
                prev : pending := back(stack)
                if prev.opcode = prefix then
                    result := PT.NewPrefix(prev.argument, result,
                                           prev.position)
                else
                    result := PT.NewInfix(prev.opcode,prev.argument,
                                          result,prev.position)
                pop_back stack


    procedure ReadSyntaxFile (P : PR.parser; syntax : text) is
    // ------------------------------------------------------------------------
    //   Read the xl.syntax file to get precedences
    // ------------------------------------------------------------------------
        type state is enumeration
            inUnknown, inInfix, inPrefix, inPostfix,
            inComment, inCommentDef,
            inText, inTextDef,
            inBlock, inBlockDef

        txt      : text
        entry    : text
        tok      : SC.token
        priority : integer    := 0
        S        : SC.scanner := SC.Open(syntax)
        stxst    : state      := inUnknown

        loop
            tok := SC.NextToken(S)
            exit if tok = SC.tokEOF

            if tok = SC.tokINTEGER then
                priority := S.integer_value

            else if (tok = SC.tokNAME or tok = SC.tokSYMBOL or
                     tok = SC.tokSTRING or tok = SC.tokQUOTE) then
                txt := S.string_value
                if txt = "NEWLINE" then
                    txt := ASCII.CR
                else if txt = "INDENT" then
                    txt := INDENT_MARKER
                else if txt = "UNINDENT" then
                    txt := UNINDENT_MARKER

                if txt = "INFIX" then
                    stxst := inInfix
                else if txt = "PREFIX" then
                    stxst := inPrefix
                else if txt = "POSTFIX" then
                    stxst := inPostfix
                else if txt = "BLOCK" then
                    stxst := inBlock
                else if txt = "COMMENT" then
                    stxst := inComment
                else if txt = "TEXT" then
                    stxst := inText
                else if txt = "STATEMENT" then
                    P.statement_priority := priority
                else if txt = "FUNCTION" then
                    P.function_priority := priority
                else if txt = "DEFAULT" then
                    P.default_priority := priority
                else if stxst = inPrefix then
                    P.prefix_priority[txt] := priority
                else if stxst = inPostfix then
                    P.postfix_priority[txt] := priority
                else if stxst = inInfix then
                    P.infix_priority[txt] := priority
                else if stxst = inComment then
                    entry := txt
                    stxst := inCommentDef
                else if stxst = inCommentDef then
                    P.comments[entry] := txt
                    stxst := inComment
                else if stxst = inText then
                    entry := txt
                    stxst := inTextDef
                else if stxst = inTextDef then
                    P.text_delimiters[entry] := txt
                    stxst := inText
                else if stxst = inBlock then
                    entry := txt
                    stxst := inBlockDef
                    P.infix_priority[entry] := priority
                else if stxst = inBlockDef then
                    P.blocks[entry] := txt
                    P.blocks[txt] := ""
                    stxst := inBlock

        SC.Close S
